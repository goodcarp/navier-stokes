import Mathlib

noncomputable section
open Real Set MeasureTheory Filter Topology
open scoped ContDiff

namespace EulerBlowup

structure SmoothForce {E : Type*} [NormedAddCommGroup E] [NormedSpace ℝ E]
    (T : ℝ) (F : ℝ × E → ℝ) : Prop where
  smooth : ContDiffOn ℝ ∞ F (Ico 0 T ×ˢ univ)
  support : ∃ Rball : ℝ, ∀ t ∈ Ico 0 T, ∀ x : E, Rball < ‖x‖ → F (t, x) = 0
  bounded : ∀ k : ℕ, ∃ M : ℝ, ∀ z ∈ Ico 0 T ×ˢ (univ : Set E),
    ‖iteratedFDerivWithin ℝ k F (Ico 0 T ×ˢ univ) z‖ ≤ M

abbrev R3 := EuclideanSpace ℝ (Fin 3)

def e3 (i : Fin 3) : R3 := EuclideanSpace.single i (1 : ℝ)

def pd3 (i : Fin 3) (g : R3 → ℝ) (x : R3) : ℝ := fderiv ℝ g x (e3 i)

def pdt3 (g : ℝ → R3 → ℝ) (t : ℝ) (x : R3) : ℝ := derivWithin (fun τ => g τ x) (Ici 0) t

def div3 (u : R3 → R3) (x : R3) : ℝ := ∑ i, pd3 i (fun y => (u y) i) x

def advect3 (u : R3 → R3) (g : R3 → ℝ) (x : R3) : ℝ := ∑ i, (u x) i * pd3 i g x

def curl3 (u : R3 → R3) (x : R3) : R3 :=
  (pd3 1 (fun y => (u y) 2) x - pd3 2 (fun y => (u y) 1) x) • e3 0 +
  (pd3 2 (fun y => (u y) 0) x - pd3 0 (fun y => (u y) 2) x) • e3 1 +
  (pd3 0 (fun y => (u y) 1) x - pd3 1 (fun y => (u y) 0) x) • e3 2

structure ClassicalEuler (T : ℝ) (u f : ℝ → R3 → R3) (p : ℝ → R3 → ℝ) : Prop where
  smoothu : ContDiffOn ℝ ∞ (fun z : ℝ × R3 => u z.1 z.2) (Ico 0 T ×ˢ univ)
  smoothp : ContDiffOn ℝ ∞ (fun z : ℝ × R3 => p z.1 z.2) (Ico 0 T ×ˢ univ)
  divFree : ∀ t ∈ Ico 0 T, ∀ x, div3 (u t) x = 0
  finiteEnergy : ∀ t ∈ Ico 0 T, MemLp (u t) 2 volume
  equ : ∀ t ∈ Ico 0 T, ∀ x, ∀ i : Fin 3,
    pdt3 (fun τ y => (u τ y) i) t x + advect3 (u t) (fun y => (u t y) i) x + pd3 i (p t) x
      = (f t x) i

structure InLipschitzClass3 (T' : ℝ) (u f : ℝ → R3 → R3) : Prop where
  lip : LocallyLipschitzOn (Icc 0 T' ×ˢ univ) (fun z : ℝ × R3 => u z.1 z.2)
  energyGrad : ∃ M, ∀ t ∈ Icc 0 T', MemLp (u t) 2 volume ∧
    (eLpNorm (u t) 2 volume).toReal ≤ M ∧ ∀ x, ‖fderiv ℝ (u t) x‖ ≤ M
  divFree : ∀ t ∈ Icc 0 T', ∀ᵐ x ∂(volume : Measure R3), div3 (u t) x = 0
  solvesWeak : ∀ φ : ℝ × R3 → R3, ContDiff ℝ ∞ φ → HasCompactSupport φ →
    tsupport φ ⊆ Ioo 0 T' ×ˢ univ → (∀ z : ℝ × R3, div3 (fun y => φ (z.1, y)) z.2 = 0) →
    ∫ z : ℝ × R3, ∑ i : Fin 3,
      ( (u z.1 z.2) i * deriv (fun τ => (φ (τ, z.2)) i) z.1
        + ∑ j : Fin 3, (u z.1 z.2) i * (u z.1 z.2) j * pd3 j (fun y => (φ (z.1, y)) i) z.2
        + (f z.1 z.2) i * (φ z) i ) = 0
end EulerBlowup

open EulerBlowup in
theorem euler_smooth_force_blowup :
    ∃ (T : ℝ) (_ : 0 < T) (u f : ℝ → R3 → R3) (p : ℝ → R3 → ℝ),
      ClassicalEuler T u f p ∧
      HasCompactSupport (u 0) ∧
      (∀ i : Fin 3, SmoothForce T (fun z => (f z.1 z.2) i)) ∧
      (∃ g : ℝ → ℝ, Tendsto g (𝓝[<] T) atTop ∧ ∀ τ ∈ Ico 0 T, ∃ x, ‖curl3 (u τ) x‖ ≥ g τ) ∧
      (∀ M : ℝ, ∃ T' ∈ Ico 0 T, ∃ g : ℝ → ℝ, (∀ τ ∈ Icc 0 T', 0 ≤ g τ ∧ ∃ x, ‖curl3 (u τ) x‖ ≥ g τ) ∧
          IntervalIntegrable g volume 0 T' ∧ ∫ τ in (0 : ℝ)..T', g τ ≥ M) ∧
      (∀ T' < T, 0 < T' → InLipschitzClass3 T' u f) ∧
      (∀ T' < T, 0 < T' → ∀ u', InLipschitzClass3 T' u' f → u' 0 = u 0 →
          ∀ t ∈ Icc 0 T', u' t = u t) := by
  sorry
